unit Regula_falsi;

interface

type
  DLLFunction = function(x : Extended) : Extended; //typ wczytywanych przez dll funkcji

function regulafalsi ( f :  DLLFunction;
                      var a,b  : Extended;
                      var fatx : Extended;
                      var st   : Integer) : Extended;
{---------------------------------------------------------------------------}
{                                                                           }
{  The function regulafalsi finds an approximate value of the root of the   }
{  equation f(x)=0 lying in the interval [a,b] by regula falsi.             }
{  Data:                                                                    }
{    a,b - the ends of the interval which contains the root (changed on     }
{          exit),                                                           }
{    f   - a Turbo Pascal function which for the given value x calculates   }
{          f(x).                                                            }
{  Results:                                                                 }
{    regulafalsi(a,b,f,fatx,st) - approximate value of the root,            }
{    fatx                       - the value of the function f(x) at the     }
{                                 approximate root.                         }
{  Other parameters:                                                        }
{    st - a variable which within the function regulafalsi is assigned the  }
{         value of:                                                         }
{           1, if a>=b,                                                     }
{           2, if f(a)*f(b)>0,                                              }
{           0, otherwise.                                                   }
{         Note: If st=1 or st=2, then regulafalsi(a,b,f,fatx,st) is not     }
{               calculated.                                                 }
{  Unlocal identifier:                                                      }
{    fx - a procedural-type identifier defined as follows                   }
{           type fx = function (x : Extended) : Extended;                   }
{  Note: A function passed as a parameter should be declared with a far     }
{        directive or compiled in the $F+ state.                            }
{                                                                           }
{---------------------------------------------------------------------------}

{---------------------------------------------------------------------------
Usage:
  The function regulafalsi finds an approximate value of the root of the
  equation f(x)=0 lying in the interval [a,b] by regula falsi.

Algorithm:
		1. Decide initial values for x1 and x2 and stopping criterion, E.
		2. Compute f1 = f(x1) and f2 = f(x2).
		3. If f1 * f2>0, x1 and x2 do not bracket any root and go to step 7;
		Otherwise continue.
		4. Compute x0 = (x1+x2)/2 and compute f0 = f(x0)
		5. If f1*f0 < 0 then
		set x2 = x0
		else
		set x1 = x0
		set f1 = f0
		6. If absolute value of (x2 – x1)/x2 is less than error E, then
		root = (x1 + x2)/2
		write the value of root
		go to step 7
		else
		go to step 4
		7. Stop.

Function call:
	binarysearch(f,a,b,mit,eps,fatx,it,st)

Data:
    a,b - the ends of the interval which contains the root (changed on
         exit),
    f   - a Turbo Pascal function which for the given value x calculates
          f(x).
Results:
  regulafalsi(a,b,f,fatx,st) - approximate value of the root,
  fatx                       - the value of the function f(x) at the
                               approximate root.
Other parameters:
  st - a variable which within the function regulafalsi is assigned the
       value of:
         1, if a>=b,
         2, if f(a)*f(b)>0,
         0, otherwise.

Types of parameters:
	f : DLLFunction
	a,b,fatx : Extended
	st : Integer

Unlocal identifier:
	DLLFunction = function(x : Extended) : Extended

Code: <below>
---------------------------------------------------------------------------}


implementation

//METODA REGULA FALSI
function regulafalsi (f :  DLLFunction;   //przekazana funkcja wczytana w main
                      var a,b  : Extended;
                      var fatx : Extended;
                      var st   : Integer) : Extended;
var sfa,sv    : Integer;
    fa,fb,v,x : Extended;
    DLL : THandle;
begin
  if a>=b
    then st:=1
    else begin
           fa:=f(a);
           if fa<0
             then sfa:=-1
             else if fa=0
                    then sfa:=0
                    else sfa:=1;
           fb:=f(b);
           if sfa*fb>0
             then st:=2
             else begin
                    st:=0;
                    x:=b-fb*(b-a)/(fb-fa);
                    while (a<x) and (x<b) do
                      begin
                        v:=f(x);
                        if v<0
                          then sv:=-1
                          else if v=0
                                 then sv:=0
                                 else sv:=1;
                        if sfa=sv
                          then begin
                                 a:=x;
                                 fa:=v
                               end
                          else begin
                                 b:=x;
                                 fb:=v
                               end;
                        x:=b-fb*(b-a)/(fb-fa)
                      end;
                    regulafalsi:=x;
                    fatx:=f(x)
                  end
         end
end;

end.
